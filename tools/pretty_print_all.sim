#!/usr/bin/env simrun

  Copyright (C) 2020 by
  Dipl.-Ing. Michael Niederle

  This program is free software; you can redistribute it and/or modify
  it under the terms of the GNU General Public License, version 2, or
  (at your option) version 3.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
  GNU General Public License for more details.

  For details of the GNU General Public License see the accompanying
  files GPLv2.txt and GLPv3.txt or
  http://www.gnu.org/licenses/gpl-2.0.html
  http://www.gnu.org/licenses/gpl-3.0.html
  or write to the
  Free Software Foundation, Inc.,
  51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.

# update all dependency files, bootstrap information and version numbers

extern $simple "compiler/simple"
$$total 0
$$updated 0
$$failed 0

$pretty_print: (folder)
  for_each directory(--SORT folder): (entry)
    $filename string(folder '/' filename_of(entry))
    if
      entry.is_a_directory:
	pretty_print filename
      :
	if filename .has_suffix. ".sim":
	  $meta_filename string(filename .without_suffix. ".sim" ".meta.sim")
	  unless file_exists(meta_filename):
	    inc &total
	    write "printing " filename " ... "
	    simple $out $err $successful "--pretty-print" filename
	    if
	      successful:
		$orig load(filename)
		if
		  out == orig:
		    writeln "unchanged"
		  :
		    inc &updated
		    save filename out
		    writeln "UPDATED"
	      :
		inc &failed
		writeln " FAILED"

$$folders list("compiler" "system" "tools" "benchmarks" "examples")
for_each_line load("configure"): (line)
  if
    ||
      line .has_prefix. "BASIC_LIBRARIES="
      line .has_prefix. "OPTIONAL_LIBRARIES="
    :
      append &folders between(line '@apos;' '@apos;').split
for_each folders: (folder) pretty_print folder
writeln "updated " updated " of " total " files"
if failed > 0: writeln "failed to print " failed " of " total " files"
