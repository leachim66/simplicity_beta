#!/usr/bin/env simrun

program_parameters
  $from
  -> std::MANDATORY_PARAMETER "from" "the text to replace"
  $to
  -> std::MANDATORY_PARAMETER "to" "the replacement text"
$$total 0
$$updated 0

$replace: (folder)
  for_each directory(--SORT folder): (entry)
    $filename string(folder '/' filename_of(entry))
    if
      entry.is_a_directory:
	replace filename
      :
	if filename .has_suffix. ".sim" || filename .has_suffix. ".c":
	  inc &total
	  write "replacing in " filename " ... "
	  load $orig filename
	  $new replace_all(orig from = to)
	  if
	    new != orig:
	      inc &updated
	      save filename new
	      writeln "UPDATED"
	    :
	      writeln "unchanged"

$$folders list("compiler" "system" "tools" "benchmarks" "examples" "runtime")
for_each_line load("configure"): (line)
  if
    ||
      line .has_prefix. "BASIC_LIBRARIES="
      line .has_prefix. "OPTIONAL_LIBRARIES="
    :
      append &folders between(line '@apos;' '@apos;').split
for_each folders: (folder) replace folder
writeln "updated " updated " of " total " files"
